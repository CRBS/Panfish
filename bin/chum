#!/usr/bin/perl


use FindBin qw($Bin);

#these allow this script to work when installed in alternate path
use lib "$Bin/../lib";
use lib "$Bin/../lib/perl5/site_perl";
use lib "$Bin/../lib/perl5";

use Getopt::Long;
use Pod::Usage;

use Panfish::ForkExecutor;
use Panfish::SSHExecutor;
use Panfish::PanfishConfig;
use Panfish::Logger;
use Panfish::PanfishConfigFactory;
use Panfish::FileReaderWriterImpl;
use Panfish::FileUtil;
use Panfish::RemoteIO;
use Cwd;

if (@ARGV == 0){
  pod2usage(2);
}


my $cmdLineParseResult = GetOptions ("path=s"           => \$pathArgValue,
                                     "deletebefore"     => \$deleteBeforeArg,
                                     "cluster=s"        => \$clusterArgValue,
                                     "skipclustercheck" => \$skipClusterCheckArg,
                                     "exclude=s"        => \@excludeArgValues,
                                     "listchummed"      => \$listChummedArg,
                                     "listexists"       => \$listExistsArg,
                                     "retry=i"          => \$retryArgValue,
                                     "timeout=i"        => \$timeOutArgValue,
                                     "dry-run"          => \$dryRunArg,
                                     "verbose+"         => \$verboseArg,
                                     "help|?"           => \$helpArg,
                                     "man"              => \$manArg) or pod2usage(2);


if ($helpArg){
  pod2usage(1);
}

if ($manArg){
  pod2usage(-exitstatus => 0, -verbose => 2);
}

if (!defined($pathArgValue)){
   pod2usage(-message => "\n --path argument must be set\n",
             -verbose => 1,
             -exitval => 2);
}

if (! -e $pathArgValue){
   pod2usage(-message => "\nPath set with --path must exist on filesystem\n",
             -verbose => 1,
             -exitval => 2);
}

# NOT IMPLEMENTED ARGS
if (defined($timeOutArgValue)){
  print STDERR "--timeout flag not implemented yet.  Please omit this argument. Sorry.\n";
  exit(100);
}

if (defined($retryArgValue)){
  print STDERR "--retry flag not implemented yet.  Please omit this argument. Sorry.\n";
  exit(100);
}

my $logger = Panfish::Logger->new();

# tab indent all logging messages
$logger->setPrefix("\t");
# sets the logging level based on number of times verbose flag 
# was passed into command
$logger->setLevelBasedOnVerbosity($verboseArg);

my $reader = Panfish::FileReaderWriterImpl->new($logger);
my $configFactory = Panfish::PanfishConfigFactory->new($reader,$logger);
my $config = $configFactory->getPanfishConfig();
my $exec = Panfish::ForkExecutor->new();
my $sshExec = Panfish::SSHExecutor->new($config,$exec,$logger);
my $uploader = Panfish::RemoteIO->new($config,$sshExec,$logger);

# if skip cluster check is set then set the $skipClusterCheckArg to 1 and pass it to
# getClusterListAsArray
if (defined($skipClusterCheckArg) && defined($clusterArgValue)){
   $skipClusterCheckArg = 1;   
}

my ($error,@clusterList) = $config->getClusterListAsArray($clusterArgValue,$skipClusterCheckArg);


my $fUtil = Panfish::FileUtil->new($logger);

# force auto flush for standard out
local $| = 1;

# if current dir is specified use it.
if ($pathArgValue eq "."){
   $pathArgValue = getcwd();  
}

# clean up the directory path
$pathArgValue = $fUtil->standardizePath($pathArgValue);

# Temporarily set logger to log to a String cause we want to have
# a clean output like this:
# Uploading to (cluster) ... (status)
# (log messages)


# let user know we don't take into account exclude arguments when getting directory sizes yet
if (@excludeArgValues){
  print "NOTE:  Exclude arguments are not taken into account when estimating data set sizes\n";
}


print "\nExamining ... $pathArgValue";
if (defined($verboseArg)){
  print "\n";
}
my $startTime = time();
my ($numFiles,$numDirs,$numSymLinks,$sizeInBytes,$error) = $fUtil->getDirectorySize($pathArgValue);

my $duration = time() - $startTime;

if (defined($error)){
   print " ... failed.  Took $duration seconds.\n";
   $logger->fatal("There was an error : $error");
   exit(1);
}

print " ... done.  Took $duration seconds.\n";

print "Found $sizeInBytes bytes in $numFiles files\n\n";

if (defined($listExistsArg)){
   $logger->info("Path to check: $pathArgValue");
}
else {
   $logger->info("Path to upload: $pathArgValue");
}
$logger->info("# of Files:  $numFiles");
$logger->info("# of Directories:  $numDirs");
$logger->info("# of Symlinks:  $numSymLinks");
$logger->info("Size in Bytes:  $sizeInBytes");

# if $pathArgValue points to a file just use directUpload
my $isFile = 0;
if ($fUtil->runFileTest("-f",$pathArgValue)){
   $isFile = 1;
}


if (defined($dryRunArg)){
   print "Dry run set, above path would be uploaded to: ";
   for (my $x = 0; $x < @clusterList; $x++){
      if ($clusterList[$x] ne $config->getThisCluster()){
           print " $clusterList[$x]";
      }
   }
   print "\n";
   exit(0);
}

my $chummedClusters = "";

my $numClusters = @clusterList;

if ($numClusters == 0){
  $logger->fatal("There are no clusters to upload to.  Please check configuration.");
  exit(1);
}

for (my $x = 0; $x < @clusterList ; $x++){

    # skip this cluster cause the data does NOT need to be moved
    if ($clusterList[$x] eq $config->getThisCluster()){
        print "Skipping $clusterList[$x] cause this program is running on this cluster\n";
        
        if ($chummedClusters ne ""){
           $chummedClusters .= ",";
        }
        $chummedClusters .= $clusterList[$x];
        next;
    }
   
    # if --listexists was set just remotely -examine path and if it exists add to cluster list
    # and go to the next cluster
    if (defined($listExistsArg)){
       print "Checking path on $clusterList[$x]";
       if (defined($verboseArg)){
          print "\n";
       }
       $startTime = time();
       my ($exists,$errorR) = $uploader->exists($pathArgValue,$clusterList[$x]);
                                                                                              

       $duration = time() - $startTime;

       if (defined($errorR)){
           print " ... failed.  Took $duration seconds.  : $errorR\n";
           next;
       }

       print " ... done.  Took $duration seconds.  ";

       if ($exists == 1){
           if ($chummedClusters ne ""){
              $chummedClusters .= ",";
           }
           $chummedClusters .= $clusterList[$x];
           print "Path exists.  Adding cluster to list\n\n";
       }
       else {
          print "Path does NOT exist.  Skipping cluster\n\n";
       }
       next;
    }

    print "Uploading to $clusterList[$x] ...";
    if (defined($verboseArg)){
        print "\n";
    }
    $startTime = time();
    if (defined($deleteBeforeArg)){
       if ($isFile == 1){
          $res = $uploader->upload($pathArgValue,$clusterList[$x],\@exludeArgValues);
       }
       else {
          $res = $uploader->deleteAndUpload($pathArgValue,$clusterList[$x],\@excludeArgValues);
       }
    }
    else {
       $res = $uploader->upload($pathArgValue,$clusterList[$x],\@excludeArgValues);
    }
    if (defined($res)){
       print "failed.\n";
       $logger->error("Unable to upload $pathArgValue to $clusterList[$x] : $res");
       next;
    }
    $duration = time() - $startTime;

    if ($duration == 0){
       $duration = 1;
    }

    printf(" done.  Transfer took $duration seconds.  Rate: %.2f Mb/sec.\n",(($sizeInBytes/1000000.0)/$duration));

    if ($chummedClusters ne ""){
        $chummedClusters .= ",";
    }
    $chummedClusters .= $clusterList[$x];
}

# used by automated programs that want to use all clusters
# so they know what clusters to download from.
if (defined($listChummedArg) || defined($listExistsArg)){
   print "chummed.clusters=$chummedClusters\n";
}

exit(0);
1;

__END__

=head1 NAME

chum - Uploads directory to remote clusters

=head1 SYNOPSIS

chum [ B<options> ] 

=head1 DESCRIPTION

B<Chum> uploads a directory to remote clusters 

B<Chum> does this utilizing B<rynsc> to push the data to clusters
specified by the user or to call clusters configured in B<panfish.config>
file.

=head1 OPTIONS


=over 4

=item B<--path path>

Defines path to data to upload.  If this is a symbolic link the code will follow the link until a real
file or directory is found and upload the data within.  Any symbolic links under that path will not be
followed.

=item B<--cluster cluster_list,..>

Defines the list of clusters (comma delimited) that the directory should be uploaded to.
If this argument is omitted then all clusters listed in B<panfish.config::queue.list> will
be used.  

=item B<--dry-run>

If set, the program will NOT transfer any data, instead the program will merely state what it 
would transfer.

=item B<--deletebefore>

Defines whether remote path is deleted before transfer is started.  Default is no.

=item B<--listchummed>

If set program will output a line listing what clusters the data was uploaded to.
The format of this output will be as follows:

chummed.clusters=CLUSTER1,CLUSTER2,CLUSTER3

=item B<--listexists>

If set program will output a line listing what clusters the specified path exists.  The
matching rules only verify remote path exists as done with -e flag in bash.

=item B<--exclude PATTERN>

Defines file/dir pattern to exclude in transfer.  Works same as B<--exclude> in B<rsync>

=item B<--retry count>

Defines the number of times to retry the transfer per cluster before failing. Default is 10.


=item B<--timeout seconds>

Defines timeout in seconds this program should wait before attempting a retry if a transfer failed.
Default is 30 seconds.

=item B<--skipclustercheck>

If set B<chum> skips the check on validity of cluster or clusters set in B<--cluster> flag.  

=item B<--verbose>

Verbose mode.  Causes B<chum> to print debugging messages.  Multiple B<--verbose> options increase
verbosity.  The maximum is 2.

=head1 EXIT STATUS

=over

=item 0     Operation was successful.

=item >0    Error.

=back

=head1 EXAMPLES

=head1 FILES

 hello

=head1 AUTHOR

Christopher Churas <churas@ncmir.ucsd.edu>

=head1 REPORTING BUGS

bugs


=head1 COPYRIGHT

blah blah

=head1 SEE ALSO

more blah



=cut
